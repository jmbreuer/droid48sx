import java.text.SimpleDateFormat

// Filename: build.gradle
// Author: Olivier Sirol <czo@free.fr>
// License: GPL-2.0 (http://www.gnu.org/copyleft)
// File Created: avril 2012
// Last Modified: Saturday 11 November 2023, 17:04
// Edit Time: 2:05:39
// Description: 48sx's build.gradle

apply plugin: 'com.android.application'

println("")
def keystorePropertiesFile = rootProject.file("local.properties")
def keystoreProperties = new Properties()
def canSign = false
if (keystorePropertiesFile.exists()) {
    keystoreProperties.load(new FileInputStream(keystorePropertiesFile))
    canSign = keystoreProperties["canSign"]
    if (canSign)
        println("-> release will be signed.")
}

int minutesSinceEpoch = System.currentTimeMillis() / 1000 / 60
println("-> minuteSinceEpoch: " + minutesSinceEpoch)
String buildDateYMD = new SimpleDateFormat("yyyyMMdd").format(new Date(System.currentTimeMillis()))
println("-> buildDateYMD:     " + buildDateYMD)
String buildDateYHM = new SimpleDateFormat("yyyy-MM-dd HH:mm").format(new Date(System.currentTimeMillis()))
println("-> buildDateYHM:     " + buildDateYHM)
def gitVersionName = "git -C ${rootDir} describe --tags --long".execute().text.trim()
def buildDateTxt

android {
    namespace 'org.czo.droid48sx'
    compileSdk 35
    //ndkVersion '25.1.8937393'
    defaultConfig {

        applicationId "org.czo.droid48sx"
        minSdkVersion 21
        targetSdk 35
        versionCode minutesSinceEpoch

        println("-> gitVersionName:   " + gitVersionName)
        setProperty("versionName", "${gitVersionName}".replaceAll(/-.*/,''))
        println("-> versionName:      " + versionName)
        println("-> versionCode:      " + minutesSinceEpoch)
        setProperty("archivesBaseName", "${applicationId}-${buildDateYMD}")
        println("-> archivesBaseName: " + archivesBaseName)
        println("")
    }

    if (canSign) {
        signingConfigs {
            release {
                storeFile file(keystoreProperties['key.store'])
                storePassword keystoreProperties['key.store.password']
                keyAlias keystoreProperties['key.alias']
                keyPassword keystoreProperties['key.alias.password']
                v1SigningEnabled true
                v2SigningEnabled true
            }
        }
    }

    buildTypes {

        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            if (canSign) {
                signingConfig signingConfigs.release
            }

            println("-> Buildtype: release")
            buildConfigField("String", "BUILD_TAG", "\"${defaultConfig.applicationId}\"")
            resValue("string", "build_tag", "${defaultConfig.applicationId}")
            println(" -> applicationId:  " + defaultConfig.applicationId)

            buildDateTxt = "Version: $defaultConfig.versionName.${minutesSinceEpoch} $name\\nGit version: $gitVersionName\\nBuild date: $buildDateYHM"
            buildConfigField("String", "BUILD_DATE", "\"$buildDateTxt\"")
            resValue("string", "build_date", "$buildDateTxt")
            println(" -> buildDateTxt:   " + buildDateTxt + "\n")
        }

        debug {
            println("-> Buildtype: debug")
            buildConfigField("String", "BUILD_TAG", "\"${defaultConfig.applicationId}\"")
            resValue("string", "build_tag", "${defaultConfig.applicationId}")
            println(" -> applicationId:  " + defaultConfig.applicationId)

            buildDateTxt = "Version: $defaultConfig.versionName.${minutesSinceEpoch} $name\\nGit version: $gitVersionName\\nBuild date: $buildDateYHM"
            buildConfigField("String", "BUILD_DATE", "\"$buildDateTxt\"")
            resValue("string", "build_date", "$buildDateTxt")
            println(" -> buildDateTxt:   " + buildDateTxt + "\n")
        }

    }

    // tasks.withType(JavaCompile) { options.compilerArgs << "-Xlint:unchecked" << "-Xlint:deprecation" }

    externalNativeBuild {
        cmake {
            version "3.22.1"
            path "src/main/cpp/CMakeLists.txt"
        }
    }
}


dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
}
